; exercise 1.27, page 55

(define (prime? n)
  (define (smallest-divisor n)
    (find-divisor n 2))
  (define (find-divisor n test-divisor)
    (cond ((> (square test-divisor) n) n)
          ((divides? test-divisor n) test-divisor)
          (else (find-divisor n (+ test-divisor 1)))))
  (define (divides? a b)
    (= (remainder b a) 0))
  (= n (smallest-divisor n)))


(define (expmod base exp m)
  (cond ((= exp 0) 1)
        ((even? exp)
         (remainder (square (expmod base (/ exp 2) m))
                    m))
        (else
         (remainder (* base (expmod base (- exp 1) m))
                    m))))

(define (fermat-test-all? n)
  (define (iter? n a)
    (cond
      ((>= a n) true)
      ((not (= (expmod a n n) a)) false)
      (else (iter? n (+ a 1)))))
  (iter? n 2))

(define (carmichael? n)
  (if (not (prime? n))
      (fermat-test-all? n)
      false))

(define (find-carmichaels limit)
  (display "Looking for Carmichael numbers up to ")
  (display limit)
  (newline)
  (define (iter n)
    (cond ((> n limit)
           (display "Done.")
	   (newline))
        (else
          (if (carmichael? n)
	    (begin (display "    Carmichael: ")
	           (display n)
	           (newline))
            (void))
         (iter (+ n 1)) )))
  (iter 2))

(find-carmichaels 3000)
